@define PYTHON2_LIBRARY %( "/usr/bin/python-dbg" %)

global pystack;
global ustack;
global stack_agg;

function extract_string(py_string) {
    return user_string(@cast(py_string, "PyStringObject", @PYTHON2_LIBRARY)->ob_sval);
}


function func_name (frame) {
    return extract_string(@cast(frame, "PyFrameObject", @PYTHON2_LIBRARY)->f_code->co_name);
}

function filename (frame) {
    return extract_string(@cast(frame, "PyFrameObject", @PYTHON2_LIBRARY)->f_code->co_filename);
}


function unpack_pystack () {
    delete pystack
    i = 0;
    frame = get_current_python_frame()
    while (frame != 0) {
        pystack[i] = sprintf("%s(%s)", func_name(frame), filename(frame));
        frame = @cast(frame, "PyFrameObject", @PYTHON2_LIBRARY)->f_back;
        i++;
    }
}

function unpack_ustack () {
    delete ustack;
    i = 0;
    bt = ubacktrace();
    addr = tokenize(bt, " ");
    while (addr != "") {
        ustack[i] = usymname(strtol(addr, 16));
        i++;
        addr = tokenize("", " ");
    }
}

function get_current_python_frame() {
    pythreadstate = @var("_PyThreadState_Current@Python/pystate.c", @PYTHON2_LIBRARY);
    return @cast(pythreadstate, "PyThreadState", @PYTHON2_LIBRARY)->frame;
}

probe timer.profile {
    if (pid() == target()) {
        unpack_pystack();
        unpack_ustack();
        merged_stack = "";
        pystack_idx = 0;
        foreach(idx in ustack) {
            if (ustack[idx] == "PyEval_EvalFrameEx") {
                merged_stack = ";" . pystack[pystack_idx] . merged_stack;
                pystack_idx++;
            } else {
                merged_stack = ";" . ustack[idx] . merged_stack;
            }
        }
        stack_agg[merged_stack] <<< 1;
    }
}


probe end {
    foreach (st in stack_agg) {
        printf("%s %d\n", st, @count(stack_agg[st]));
    }
}


probe timer.s(10) { exit(); }
