@define PYTHON_LIBRARY %( @1 %)

global pystack;
global ustack;
global stack_agg;


function py3_extract_string(py_unicodeobject) {
    // The actual character buffer is stored right after the PyASCIIObject
    // struct. See Include/unicodeobject.h in the Python source for details.
    // TODO: assuming ASCII here
    data = &@cast(py_unicodeobject, "PyASCIIObject", @PYTHON_LIBRARY)[1];
    return user_string(data);
}


function func_name (frame) {
    return py3_extract_string(@cast(frame, "PyFrameObject", @PYTHON_LIBRARY)->f_code->co_name);
}

function filename (frame) {
    return py3_extract_string(@cast(frame, "PyFrameObject", @PYTHON_LIBRARY)->f_code->co_filename);
}


function unpack_pystack () {
    delete pystack
    i = 0;
    frame = current_python_frame()
    while (frame != 0) {
        pystack[i] = sprintf("%s(%s)", func_name(frame), filename(frame));
        frame = @cast(frame, "PyFrameObject", @PYTHON_LIBRARY)->f_back;
        i++;
    }
}

function unpack_ustack () {
    delete ustack;
    i = 0;
    bt = ubacktrace();
    addr = tokenize(bt, " ");
    while (addr != "") {
        ustack[i] = usymname(strtol(addr, 16));
        i++;
        addr = tokenize("", " ");
    }
}


function current_python_frame() {
    addr = &@var("_PyThreadState_Current@Python/pystate.c", @PYTHON_LIBRARY);
    v = @cast(addr, "_Py_atomic_address", @PYTHON_LIBRARY)->_value;
    pythreadstate = @cast(v, "PyThreadState", @PYTHON_LIBRARY)->frame;
    return pythreadstate;
}


function sprint_merged_stack () {
    unpack_ustack();
    try {
        unpack_pystack();
        have_pystack = 1;
    } catch {
        have_pystack = 0;
    }
    merged_stack = "";
    pystack_idx = 0;
    foreach(idx+ in ustack) {
        if ((ustack[idx] == "PyEval_EvalFrameEx") && have_pystack) {
            merged_stack = ";" . pystack[pystack_idx] . merged_stack;
            pystack_idx++;
        } else {
            merged_stack = ";" . ustack[idx] . merged_stack;
        }
    }
    return merged_stack;
}

function sprint_pystack () {
    unpack_pystack();
    stack = "";
    foreach (idx+ in pystack) {
        stack = ";" . pystack[idx] . stack;
    }
    return stack;
}

probe timer.profile {
    if (pid() == target()) {
            stack_agg[sprint_merged_stack()] <<< 1;
    }
}


probe end {
    foreach (st in stack_agg) {
        printf("%s %d\n", st, @count(stack_agg[st]));
    }
}


probe timer.s($2) { exit(); }
